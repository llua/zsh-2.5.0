# zed():  Peter Stephenson <pws@s-a.amtp.liv.ac.uk>
# No other shell could do this.
# Edit small files with the command line editor.
# Use ^X^W to save, ^C to abort.
# Option -f: edit shell functions.  (Also if called as fned.)
#
# Completion: use
# compctl -f -x 'w[1,-f]' -F -- zed

local var nb fun

[[ $1 = -f || $0 = fned ]] && fun=1
[[ $1 = -(|-|f) ]] && shift

[[ -z "$1" ]] && echo 'Usage: "zed filename" or "zed -f function"' && return 1

# catch interrupts
TRAPINT() {
  bindkey "^M" accept-line
  [[ $nb = 1 ]] || unsetopt nobanghist
  trap - INT
  (($1 > 0)) && return $[128+$1]
}

# don't mangle !'s
[[ -o nobanghist ]] && nb=1
setopt nobanghist

bindkey "^M" self-insert-unmeta
# Depending on your stty's, you may be able to use ^J as accept-line, else:
bindkey "^X^W" accept-line

if [[ $fun = 1 ]]; then
  var="$(functions $1)"
  # If function is undefined but autoloadable, load it
  if [[ $var = undefined* ]]; then
    local dir
    for dir in $fpath; do
      if [[ -f $dir/$1 ]]; then
	var="$1() {
$(<$dir/$1)
}"
	break
      fi
    done
  elif [[ -z $var ]]; then
    var="$1() {
}"
  fi
  vared var
  # remember there\'s a memory leak in eval...
  eval function "$var"
else
  [[ -f $1 ]] && var="$(<$1)"
  vared var
  (( $? == 0 )) && print -R "$var" >! $1
fi

bindkey "^X^W" undefined-key

# Restore setup
TRAPINT 0
return 0

# End of zed
